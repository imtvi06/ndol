      SUBROUTINE CARDIN (JC,NG,aw,nw,NN,RT,IT,ND,*,NCDS,IRD,IPD,IECHO)
*      IMPLICIT INTEGER*2 (I-N)

C   READS A CARD OR CARDS USING FREE FORMAT

C  THIS SUBROUTINE IS PART OF MCDF PACKAGE ; SEE I.P. GRANT ET AL.,
C  COMPUTER PHYS. COMMUN., 21(1980)207 FOR DETAILS.

C  ADAPTED FOR FORTRAN 77 FACILITIES IN REAL TIME MICROCOMPUTERS
C  BY LUIS MONTERO, FACULTAD DE QUIMICA, UNIVERSIDAD DE LA HABANA, 1987.
C  Improved for string search by Luis A. Montero, FACULTAD DE QUIMICA,
C  UNIVERSIDAD DE LA HABANA,1995.

C   INPUT

C   JC - DETERMINES THE FORMAT IN WHICH THE CARD IS TO BE READ
C      =0 READS 80 ALPHANUMERIC CHARACTERS WHICH ARE TO BE TRANSFORMED
C         IN NUMERIC DATA APPEARING IN ARRAYS <RT> OR/AND <IT>. SEE
C         BELOW
C      =1 a line with 80 charachers, to be used as a title or to be
c         identified word by word.
C      =2 READS A FOUR CHARACTER (A4) LABEL PLUS 72 ALPHANUMERIC
C         CHARACTERS IN A CARD OR LINE, WHICH ARE TO BE TRANSFORMED
C         IN NUMERIC DATA AS IN JC=0
C   ND - MAXIMUM NUMBER OF NUMBERS WHICH CAN BE READ.
C        SETS THE DIMENSIONS OF RT AND IT

C   OUTPUT

C   NG - A4 LABEL READ IF JC=2 OR FULL line of text of 80 characters long
C        IF JC=1
c   aw - an array of 18 words up to 32 characters each one: aw(18,32) to
c        be dimensioned in the calling programs as character*32 aw(18)
c        when jc=1
c   mw - number of words identified in the line when jc=1
C   NN - THE NUMBER OF NUMBERS WHICH HAVE BEEN READ
C   RT - ARRAY CONTAINING THE NN NUMBERS AS REALS
C   IT - ARRAY CONTAINING THE NN NUMBERS AS INTEGERS
C   NCDS IS THE NUMBER OF THE CURRENT INPUT CARD


C   THE FOLLOWING CHARACTERS ARE RECOGNISED
C   0 1 2 3 4 5 6 7 8 9 - . E / C  AND BLANK
C   + IS RECOGNISED ONLY IN SEQUENCE 'E+'

C   BLANK IS USED TO SEPARATE NUMBERS
C   BLANKS CANNOT OCCUR WITHIN A NUMBER
C   'C' CAN BE USED TO CONTINUE A CARD - ANOTHER CARD WILL
C   BE READ BY CARDIN USING FORMAT JC=0
C   '/' CAN BE USED TO INPUT FRACTIONS WITH NUMERATOR OR
C   DENOMINATOR BEING REAL OR INTEGER
C   'E' DENOTES EXPONENT
C   THE EXPONENT MUST BE AN INTEGER BETWEEN -99 AND 99

C   SOME CHECKS ON CONSISTENCY ARE IN THE CODE
C   SEE BELOW FOR A DESCRIPTION OF ERRORS WHICH WILL
C   TERMINATE THE INPUT

C   THE FOLLOWING ARE EXAMPLES OF NUMBERS WHICH CAN
C   BE READ BY CARDIN
C   .123 , -.123 , -3.23 , 4.5E6 , 12 , 2E-6
C   2/6 , 2.1/6 , -2./6E-2

C   NUMBERS ARE READ AS REALS (RT)
C   AND THEN THE NEAREST INTEGERS ARE OBTAINED (IT)

C  NO SUBROUTINES CALLED.

      DIMENSION RT(ND),IT(ND)
      CHARACTER*1 ICH(16),JD(81),JDJ,PLUS,NG(80),aw(32,18)
*      CHARACTER*4 NG(ND)
      PARAMETER (ZERO=0., HALF=.5, ONE=1., TEN=10., CON=1.E-6)

      DATA ICH /'0','1','2','3','4','5','6','7','8','9','-','.',
     .          'E','/',' ','C'/, JD(81) /' '/, PLUS/'+'/

      DO 10 I=1,ND
      IT(I)=0
   10 RT(I)=ZERO
      NN=0
      N1=1
      IF (JC.EQ.0) GO TO 20
      IF (JC.EQ.1) GOTO 480

C  JC=2   READ LABEL(A4) + 72 CHARACTERS (A TOTAL OF 80 CHARACTERS)

      READ (IRD,510,END=500) (NG(j),j=1,4),(JD(I),I=5,80)
      NCDS=NCDS+1
      IF(IECHO.NE.0)THEN
        WRITE (IPD,520) NCDS,(NG(j),j=1,4),(JD(I),I=5,72),IRD
      ENDIF
      J=4
      GO TO 30

C  JC=0   READ 80 CHARACTERS TO BE TRANSFORMED IN NUMERIC DATA

   20 READ (IRD,530,END=500) (JD(I),I=1,80)
      NCDS=NCDS+1
      IF(IECHO.NE.0) THEN
        WRITE (IPD,540) NCDS,(JD(I),I=1,72),IRD
      ENDIF
      J=0

C   BEGIN HERE FOR EACH NEW CARD

   30 ICO=0

C   BEGIN HERE WHEN A COMPLETE NUMBER HAS BEEN READ

   40 IBL=0
      IDI=0

C   BEGIN HERE IF '/' HAS BEEN READ - READ DENOMINATOR

   50 IEX=0

C   BEGIN HERE IF 'E' HAS BEEN READ  - READ EXPONENT

   60 ISI=0
      IPT=0
      JPT=0
      SUM=ZERO
      SIGN=ONE

C   EXAMINE EACH CHARACTER IN TURN

   70 J=J+1
      IF (J-81) 80,80,430
   80 JDJ=JD(J)
      DO 90 ILOOP=1,16
        I=ILOOP
        IF (JDJ.EQ.ICH(I)) GO TO 100
   90   CONTINUE

C   CHECK FOR THE SEQUENCE 'E+'

      IF(JDJ.EQ.PLUS) GOTO 60

C   ERROR - CHARACTER NOT ALLOWED

      GO TO 460
  100 IF (I-15) 130,370,110

C   CHARACTER IS 'C' FOR CONTINUE - ANOTHER CARD WILL BE READ
C   ERROR - IF IBL=1 (NUMBER IS BEING READ)
C   'C' CANNOT OCCUR WITHIN A NUMBER

  110 IF (IBL) 120,120,460
  120 ICO=1
      GO TO 70

C   CHARACTER IS NOT ' ' OR 'C'
C   ERROR - IF A 'C' HAS ALREADY BEEN READ
C   ONLY ' ' OR 'C' CAN FOLLOW A 'C' ON THE CARD
C   ALL CHARACTERS FOLLOWING 'C' ARE EXAMINED

  130 IF (ICO) 140,140,460
  140 IF (I-10) 150,150,160

C   CHARACTER IS A DIGIT
C   CONSTRUCT THE NUMBER
C   IBL IS SET TO 1 AND ISI IS SET TO 1 TO INDICATE A NUMBER
C   IS BEING READ

  150 IBL=1
      ISI=1
      JPT=JPT+1
      SUM=SUM*TEN+DBLE(I-1)
      GO TO 70
  160 IF (I-11) 170,170,190

C   CHARACTER IS '-'
C   ERROR - IF A SIGN HAS ALREADY BEEN READ FOR THIS NUMBER

  170 IF (ISI) 180,180,460
  180 SIGN=-ONE
      ISI=1
      GO TO 70
  190 IF (I-12) 200,200,220

C   CHARACTER IS '.'
C   ERROR - IF '.' HAS ALREADY BEEN READ FOR THIS NUMBER

  200 IF (IPT) 210,210,460
  210 ISI=1
      IPT=1
      JPT=0
      GO TO 70

C   CHARACTER IS 'E' '/' OR ' '
C   FORM CURRENT NUMBER
C   ERROR - '.' IN ISOLATION

  220 SUM=SIGN*SUM
      IF (IPT) 250,250,230
  230 IF (IBL) 460,460,240
  240 SUM=SUM*TEN**(-JPT)
  250 IF (I-13) 260,260,290

C   CHARACTER IS 'E'
C   ERROR - IF 'E' HAS ALREADY BEEN READ FOR THIS NUMBER
C           OR A NUMBER IS NOT BEING READ
C   STORE MANTISSA IN SUMA AND LOOK FOR EXPONENT

  260 IF (IEX) 270,270,460
  270 IF (IBL) 460,460,280
  280 IEX=1
      SUMA=SUM
      GO TO 60

C   CHARACTER IS '/' OR ' '

  290 IF (IEX) 330,330,300

C   'E' HAS BEEN READ FOR THIS NUMBER
C   THE EXPONENT HAS NOW BEEN OBTAINED
C   ERROR - IF EXPONENT CONTAINED '.' OR MORE THAN 2 DIGITS
C   EXPONENT IS AN INTEGER LYING BETWEEN -99 AND 99
C   FORM THE NUMBER

  300 IF (IPT) 310,310,460
  310 IF (JPT-2) 320,320,460
  320 IF (SUM.LT.ZERO) SUM=SUM-HALF-CON
      IF (SUM.GE.ZERO) SUM=SUM+HALF+CON
      JPT=INT(SUM)
      SUM=SUMA*TEN**JPT
  330 IF (I-14) 340,340,390

C   CHARACTER IS '/'  - DENOTING DIVISION
C   ERROR - IF '/' HAS ALREADY BEEN READ FOR THIS NUMBER
C           OR A NUMBER IS NOT BEING READ
C   STORE THE FIRST NUMBER
C   READ THE DENOMINATOR

  340 IF (IDI) 350,350,460
  350 IF (IBL) 460,460,360
  360 IDI=1
      SUMA=SUM
      GO TO 50

C   CHARACTER IS ' '
C   THIS CHARACTER IS USED TO SEPARATE NUMBERS
C   IT INDICATES THAT A NUMBER HAS BEEN READ

  370 IF (IBL) 380,380,220

C   ERROR - '-' OCCURS IN ISOLATION

  380 IF (ISI) 70,70,460

C   '/' WAS READ   EVALUATE NUMBER
C   ERROR - DENOMINATOR IS ZERO

  390 IF (IDI) 420,420,400
  400 IF (SUM) 410,460,410
  410 SUM=SUMA/SUM
  420 NN=NN+1

C ... NEXT CARD REMOVED. MAKE SURE SIZE OF IT AND RT LARGE ENOUGH.
C ... NOW CARDIN IS USING THE FIRST "ND" READ NUMBERS ONLY
*      IF (NN.GT.ND) GO TO 470

      RT(NN)=SUM
      GO TO 40

C   CARD HAS BEEN READ
C   RETURN IF NO NUMBERS WERE ON CARD
C   READ ANOTHER CARD IF 'C' HAS BEEN READ
C   IF A CARD IS TO BE CONTINUED THERE MUST BE
C   AT LEAST 1 NUMBER ON IT

  430 IF (NN.LT.N1) RETURN
      IF (ICO) 440,440,20

C   THE NUMBERS ARE READ AS REAL
C   TAKE THE NEAREST INTEGERS

  440 DO 450 I=N1,NN
      SUM=RT(I)
      IF (SUM.LT.ZERO) SUM=SUM-HALF-CON
      IF (SUM.GE.ZERO) SUM=SUM+HALF+CON
  450 IT(I)=INT(SUM)
      RETURN

  460 WRITE (IPD,550)
      RETURN 1
*  470 WRITE (IPD,560)
*      WRITE (*,560)
*      RETURN 1

  480 CONTINUE

C  JC=1 READ TITLE CARD

      READ (IRD,610,END=500) NG
      do i=80,1,-1
        if (.not.(ichar(ng(i)).le.32 .or. ichar(ng(i)).gt.127)) then
          leng = i
          goto 485
        endif
      enddo
485   nw = 0
      iw = -1
      do i=1,leng
        if (ng(i).gt.char(32) .and. ng(i).le.char(127)) then
          if (iw.lt.0) then
            iw = 0
            nw = nw + 1
          endif
          iw = iw + 1
          aw(iw,nw) = ng(i)
        else
          if (iw.ge.0) iw = -1
        endif
      enddo
      NCDS  = NCDS + 1
      IF(IECHO.NE.0) THEN
        WRITE(IPD,620) NCDS,(NG(I),I=1,leng),IRD
      ENDIF
      RETURN

  500 CONTINUE

C  HERE IF EOF ENCOUNTERED ON IRD
      WRITE (IPD,630) IRD
      RETURN 1

  510 FORMAT (A4,76A1)
  520 FORMAT (1X,I3,3H =>,A4,68A1,1H<,2X,4HFILE,I3)
  530 FORMAT (80A1)
  540 FORMAT (1X,I3,3H =>,72A1,   1H<,2X,4HFILE,I3)
  550 FORMAT (/' *** REVISE LA TARJETA DE DATOS LEIDA POR CARDIN ANTERIO
     .RMENTE ***')
*  560 FORMAT (/' *** DIMENSION ERROR FOR ND IN CARDIN ***')
  610 FORMAT (80a1)
  620 FORMAT (1X,I3,3H =>, 18A4,   1H<,2X,4HFILE,I3)
  630 FORMAT (/' *** ENCONTRADO UN FIN DE FICHERO EN LA UNIDAD DE ENTRAD
     .A ',I3,' ***')
      END
